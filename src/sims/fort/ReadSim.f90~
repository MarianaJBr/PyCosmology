include "SimOperations.f90"
include "jacobi.f"

module SimOps
	use SimOperations
	implicit none


	real, allocatable		:: pos(:,:),vel(:,:)
	real, allocatable		:: g_pos(:,:), g_vel(:,:)
	integer,allocatable  	:: ID(:)
	integer,allocatable  	:: grouplen(:), groupoffset(:), ids(:),group_IDS(:)
	integer, allocatable	:: group_number(:)
	
	real, allocatable 		:: pos_r(:,:), r(:), vel_r(:,:)
	real(8),allocatable   	:: dens(:), bin_edges(:), vol(:)
	
    
	!Variables defined in the header of a snapshot file.	
	integer  :: FlagSfr, FlagFeedback, Nall(6), FlagAge, FlagCooling 
	integer  :: FlagMetals, NallHW(6), FlagEntrICs, npart(6),NumFiles

	real(8)  ::massarr(6), time, BoxSize,OmegaLambda
	real(8)	 ::HubbleParam,Omega0, Redshift
	
	contains


  !----------------------------------------------------------------------
 	  ! ReadSim - Simply read in information from the simulation
  !----------------------------------------------------------------------		
    subroutine ReadSim(filename)    
		integer :: i
		character(len=*), intent(in) :: filename

		! Read in Simulation File values (only positions, velocities and ID's)  
		open(unit=10,file=filename,form='UNFORMATTED')
        
		read(10) npart, massarr, time, Redshift, FlagSfr, FlagFeedback, Nall,&
				  & FlagCooling, NumFiles, BoxSize, Omega0, OmegaLambda,&
				  &HubbleParam,FlagAge, FlagMetals, NallHW, FlagEntrICs
																							
		write(*,*) "Number of particles(1) = ", npart(1)
		write(*,*) "Number of particles(2) = ", npart(2)
		write(*,*) "Number of particles(3) = ", npart(3)
		write(*,*) "Number of particles(4) = ", npart(4)
		write(*,*) "Number of particles(5) = ", npart(5)
		write(*,*) "Number of particles(6) = ", npart(6)
		write(*,*) "Time of snapshot: ", time
		write(*,*) "Redshift of snapshot: ", Redshift
		write(*,*) "BoxSize of Sim: ", BoxSize
		
		allocate(pos(3,npart(2)),vel(3,npart(2)),ID(npart(2)))
		read(10)
		read(10)
				
		
		read(10) (ID(i), i=1,npart(2))
		write(*,*) "Read Particle ID's"
		rewind(10)
		read(10)
		read(10) (pos(1,ID(i)),pos(2,ID(i)),pos(3,ID(i)), i=1,npart(2))
		write(*,*) "Read Positions"
		read(10) (vel(1,ID(i)),vel(2,ID(i)),vel(3,ID(i)), i=1,npart(2))
		write(*,*) "Read Velocities"
		close(10)
      
      deallocate(ID)
    end subroutine
    
  !----------------------------------------------------------------------
 	  ! find_groups - establish which objects belong to which groups.
  !----------------------------------------------------------------------
    subroutine find_groups(filename,ids_file,loud)  
    
    	integer 			:: ngroups, k, ntot, group_offset, group_size
    	character(len=*)	:: filename,ids_file
    	logical, intent(in), optional	::loud
    	
    	integer				::offset_pos(1), group_ID, i
    	logical				::temp_loud
    	
    	
    	if (present(loud)) then
    		temp_loud = loud
    	else
    		temp_loud = .False.
    	end if
    	
    	open(1,file=filename,status='unknown',form='unformatted')
    	          
    	read(1) ngroups                         ! Total number of FOF groups
    	
    	write(*,'(a,i6,a)') 'Reading ',ngroups,' groups...'
    	
    	allocate(grouplen(ngroups),groupoffset(ngroups))
    	
    	read(1) (grouplen(i),i=1,ngroups)      ! Length of each FOF group
    	write(*,*) 'Read group lengths'
    	read(1) (groupoffset(i),i=1,ngroups)  ! Offset in the pos.ids file
    	write(*,*) 'Read group offsets'
    	!read(1) (nsubgroups(i),i=1,ngroups) ! Number of subgroups -- ignore
    	!write(*,*) 'Read number of subgroups'
    	close(1)
    	
    	open(1,file=ids_file,status='unknown',form='unformatted')
    	read(1) ntot                     ! Total number of particles in FOF groups
    	allocate(ids(ntot))
    	read(1) (ids(i),i=1,ntot)  ! Particle IDs
    	close(1)    

        allocate(group_number(size(pos(1,:))))
        group_number(:) = 0
    
    ! Locate Largest Group/Halo and save properties.
        do k = 1,ngroups
        	if(temp_loud)then
        		write(*,*) "Calculating characteristics of group", k
        	end if
          offset_pos = maxloc(grouplen)
          group_offset = groupoffset(offset_pos(1))
          group_size = maxval(grouplen)
          if (temp_loud)then
          	  write(*,*) "Group Size: ", group_size
          end if



          if (temp_loud) then
          	  write(*,*) "Specifying group particles"
          end if
          
          do i=1,maxval(grouplen)
            group_ID = ids(group_offset+i)
            
            group_number(group_ID) = k
     
          end do
          
          grouplen(offset_pos(1)) = -1
        end do
      end subroutine
 
  !----------------------------------------------------------------------
 	  ! centre - change co-ords of a group to its centre
  !----------------------------------------------------------------------        
        
  	subroutine centre(g_pos,n,mode)
  	!
  	!mode refers to where to place the centre (0,0,0).
  	!mode = 1 uses the first particle (centre of density)
  	!mode = 2 uses the average particle position (centre of mass)
  	!mode = 3 recentres with equal limits in both directions.
  	!
  	
    	integer,intent(in)	:: n 
    	integer,intent(in),optional :: mode
  		real,intent(inout)	:: g_pos(3,n)
  		
  		real				:: centre_of_density(3), c_o_m(3), equal_centre(3)
  		integer				:: i, temp_mode
 		
  		if (present(mode)) then
  			temp_mode = mode
  		else
  			temp_mode = 1
  		end if
  		
        centre_of_density = g_pos(:,1)
        ! Re-centre the group around the centre of density
        call ReCentre(g_pos, real(BoxSize),centre_of_density)
        
        write(*,"(A7,3es20.8,A1)") "CoD = (",&
                                    &centre_of_density,")"
        
        if (temp_mode == 2) then
        	
			c_o_m(:) = 0.000
			do i = 1,n
			  c_o_m(1) = c_o_m(1) + g_pos(1,i)
			  c_o_m(2) = c_o_m(2) + g_pos(2,i)
			  c_o_m(3) = c_o_m(3) + g_pos(3,i)
			end do
			c_o_m(:) = c_o_m(:)/real(n)
			write(*,"(A27,3es20.8,A1)") "Centre of Mass of group = (", c_o_m,")"
			! Re-centre the group around the centre
				call ReCentre(g_pos, real(BoxSize),c_o_m)
		end if
        
		if (temp_mode == 3) then
			equal_centre(1) = (maxval(g_pos(1,:))+minval(g_pos(1,:)))/2.d0
			equal_centre(2) = (maxval(g_pos(2,:))+minval(g_pos(2,:)))/2.d0
			equal_centre(3) = (maxval(g_pos(3,:))+minval(g_pos(3,:)))/2.d0
			! Re-centre the group around the centre
				call ReCentre(g_pos, real(BoxSize),equal_centre)
		end if
		
        write(*,*) "Group Re-centred"
     end subroutine
     
            
  
  !----------------------------------------------------------------------
 	  ! rotate - rotate data into moment of inertia defined coords plus angle
  !----------------------------------------------------------------------            
	subroutine rotate(g_pos, g_vel, n, u, theta)
		integer, intent(in)	::n
		real, intent(inout)	::g_pos(3,n), g_vel(3,n)
		real, intent(in)	::u(3),theta
		
		integer	   :: nrot, i
    	real      :: IT(3,3), eigval(3), pos_r(3,n)
		real      :: eigvec(3,3),axis_ratio_b, axis_ratio_c, bin_size
	
		
            ! Calculate the Moment of Inertia tensor
            IT = Inertia(real(massarr(2)),g_pos)
            write(*,*) "Moment of Inertia calculated"
          
             write(*,*) "Moment of Inertia tensor:"
             do i=1,3
                write(*,*) IT(i,:)
             end do
             
            ! Diagonalize Inertia Tensor
              call jacobi(IT,3,3,eigval,eigvec,nrot)
              write(*,*) "Moment of Inertia tensor diagonalized"
            
            ! Re-order the eigs
              call EigOrder(eigval,eigvec)
              
            ! Return the ratios of axes
              axis_ratio_b = sqrt(eigval(2)/eigval(1))
              axis_ratio_c = sqrt(eigval(3)/eigval(1))
              write(*,*) "Ratio of axes lengths b/a = ", axis_ratio_b
              write(*,*) "Ratio of axes lengths c/a = ", axis_ratio_c
              
            ! Project data into co-ordinates defined by
                ! xbar = eigvec(1)*(x,y,z)
                ! ybar = eigvec(2)*(x,y,z)
                ! zbar = eigvec(3)*(x,y,z)
                  
              do i=1,n
                g_pos(:,i) = matmul(eigvec,g_pos(:,i))
                g_vel(:,i) = matmul(eigvec,g_vel(:,i))
              end do
              
              call GenRotate(g_pos,u,theta,pos_r)
              g_pos = pos_r

              call GenRotate(g_vel,u,theta,pos_r)
              g_vel = pos_r
      end subroutine
      
      
  !----------------------------------------------------------------------
 	  ! density_profile - calculate density profile.
  !----------------------------------------------------------------------  
   	subroutine	density_profile(g_pos, n,bins,min_bin,dens,bin_edges)
   		integer, intent(in)	::n, bins
   		real, intent(in)	::g_pos(3,n),min_bin
   		
   		real, intent(out)				::bin_edges(bins+1),dens(bins)
   		real							::r(n), vol(bins)
   		integer							::i
   		
       ! Calculate radii from c_o_m/c_o_d (to make density profiles)
         do i=1,n
           r(i) = sqrt(g_pos(1,i)**2 + g_pos(2,i)**2+ g_pos(3,i)**2)
         end do
         
       ! Calculate Density Profile.
         call LogBins(r,bins,min_bin,dens,bin_edges)
         dens = dens*massarr(2)
         
         do i=1,bins
           vol(i) = (4.0*3.14159*(bin_edges(i+1)**3-bin_edges(i)**3))/3.0
         end do
         
         dens(:) = dens(:)/vol(:)
         write(*,*)"Calculated spherically symmetric density profile"
     end subroutine
         


    
end module
     

